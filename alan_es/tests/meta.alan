-- ***********************************************
--   META MESSAGES TEST by Ricardo Osio (2021)
-- ***********************************************

-- This adventure is for testing "meta" stuff:

--  * Spanish run-time MESSAGEs, to check they are translated properly and use
--    correct GNA references.
--  * META VERBs, to ensure they are not consuming turns.
--  * Turn consumptions in general, to check how ARun handles player input which
--    is malformed, ambiguous or not understood for other reasons.

-- Turn consumption is tracked via a "ticker machine" that can be turned ON/OFF
-- by pressing a red button. When the ticker is on, it will tick at every turn
-- and display a counter of the turns that have elapsed since its activation
-- (the counter is reset when the ticker is stopped).

-- We've added the activation button since we don't want to ticker to be always
-- running, because in some test we'll be only checking that the run-time
-- messages are translated and rendered properly, so having the ticker ON all
-- the time would clutter the transcript unnecessarily.

-- This test adventure is mostly based on the "meta-verbs" tests from the ALAN
-- Standard Library. Some features from the StdLib are not included in the
-- Foundation Library, therefore not all the original tests are implemented
-- here.

-- =============================================================================

Import 'Library.i'.

--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
--                         S A L A   D E   P R U E B A S
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- The "test room" is where ordinary META VERBs and Run-Time MESSAGEs can be
-- safely tested and elicited. Various tech devices and ad hoc objects are made
-- available on site for the purpose.

The salaDePrueba IsA location
  Name 'Sala de pruebas'
  Description
    "Aquí se prueban diversos aspectos del lenguaje ALAN,
     gracias a tecnología de punta y sujetos de prueba
     extremadamente competentes (como tú).
     To the north lies the quarantine zone." -- @TRANSLATE!
  Exit norte to bugsRoom.
End The.


-- =============================================================================
--                                   S C O R E
-- =============================================================================

-- A scoreboard to test score and some verbs related.

The tablaDePuntos IsA object at salaDePrueba
  Has artículo "la".
  Name 'tabla de puntuaciones'
  Name scoreboard
  Name tabla
  Is Not tomable.
  Is Not empujable.
  Verb examinar
    Does Only "La tabla de puntuaciones muestra" Say score.
      If score = 1 Then
        Say "punto.".
      Else
        Say "puntos.".
      End If.
  End Verb examinar.
End The.

-- =============================================================================
--                                  D R I N K S
-- =============================================================================

-- These drinks are used to test the score. Drinking them rewards the player
-- with points.

-- == The "bebida" class ==
Every bebida IsA object
  Is bebible.
End Every.

-- == refresco (a soda) ==
The refresco IsA bebida at salaDePrueba
  Name refresco.
  Has xDesc "Un refresco de cola.
             Probablemente delicioso, pero con mucha azúcar.".

  Verb beber
    Does After Score 1.
  End Verb.
End The.

-- == café (coffee) ==
The café IsA bebida at salaDePrueba
  Name café. Name cafe.
  Has xDesc "Un poco de café caliente.
             Te vendría bien para esa cara de sueño...".

  Verb beber
    Does After Score 1.
  End Verb.
End The.

-- == jugo de naranja (orange juice) ==
The jugo IsA bebida at salaDePrueba
  Name 'jugo de naranja'. Name jugo.
  Has xDesc "Un refrescante jugo de naranja.
             ¡La vitamina C es muy beneficiosa!".

  Verb beber
    Does After Score 5.
  End Verb.
End The.

-- == agua (a glass of water) ==
The agua IsA bebida at salaDePrueba
  Has artículo "el".
  Is femenina.
  Name agua. Name 'vaso de agua'.
  Has xDesc "Un vaso de agua increíblemente saludable.".

  Verb beber
    Does After Score 200.
  End Verb.
End The.

-- =============================================================================
--                      T H E   T I C K E R   M A C H I N E
-- =============================================================================

-- We need a ticker to track that META VERBs are not consuming a turn.

-- So we implement a "tricker machine" that can be activated and stopped by
-- pressing a red button, and will start ticking in the background at every
-- turn, letting us know when a turn was consumed (EVENTs are only executed
-- during consumed game turns, not when executing META VERBS).

-- The ticker counter is implemented via the 'cnt' attribute on the button.
-- Pressing the button when the ticker is active will stop the ticker and reset
-- the ticks counter.

--------------------------------------------------------------------------------
-- V E R B  " P R E S S "
--------------------------------------------------------------------------------

-- @CHECK: @Rich15 please confirm if this is correct (if yes, delete this line):

-- We need a dedicated verb for pressing the button, since the "empujar" verb
-- for "pushing" things is not used for pressing buttons in Spanish
-- (unlike English, were we can say "push the button").

Add to every thing
  Is not presionable. -- i.e. not pressable.
End add to thing.

Synonyms -- @CHECK: @Rich15 please look is these syntaxes are OK!
  presiona, presione, pulsa,  pulse, pulsar =  presionar.

Syntax
  presionar =  presionar (obj)
    Where obj IsA object
      else "You can only press objects!" -- @TRANSLATE!

Add to every object
  Verb presionar
    Check obj is empujable
      else "No puedes presionar $+1."
    Does
      "You press $+1, but nothing happens." -- @TRANSLATE!
  End verb.
End add to.

--------------------------------------------------------------------------------
-- T I C K E R   B U T T O N
--------------------------------------------------------------------------------

The botón IsA object at salaDePrueba
  Name botón. Name boton.
  Name botón rojo. Name boton rojo.
  Is presionable.
  Is not tomable.
  Is not active. -- i.e. the ticker machine state.
  Has cnt 0. -- The ticks counter.
  Description "A red button is bolted in the wall." -- @TRANSLATE!
  Has xDesc "It's a really big and menacing red button.
             Above the button there's a sign.". -- @TRANSLATE!

  Verb presionar
    Does only
      "As you press the button" -- @TRANSLATE!
      If this is not active
        then
          "you ear a humming sound coming from the wall,
           as if a hidden machine came to life." -- @TRANSLATE!
          Schedule ticker at hero after 0.
          Set this:cnt to 0. -- reset counter.
          Make this active.
        else -- @TRANSLATE:
          "the ticker machine stops and the humming from the walls ceases."
          Cancel ticker.
          Make this not active.
      End if.
  End verb.
End The.

------------------------
-- B U T T O N   S I G N
------------------------

-- @TODO: Once we fix scenery in the English and Spanish libraries, the 'cartel'
--        should be made into a scenery (probably will be an attribute by then,
--        not a class any longer).

The cartel IsA object at salaDePrueba
  Is not tomable.
  Is leible. -- @TODO: Implement reading (need to check if EN lib uses xDesc)
  Description "" -- Don't mention it in room descriptions!
  Has xDesc "The sign reads ""Press button to toggle the
             ticker machine on/off"".". -- @TRANSLATE!
End The.

--------------------------------------------------------------------------------
-- T I C K E R   E V E N T
--------------------------------------------------------------------------------

-- This event will let us know when a turn has been consumed.

Event ticker
  Increase botón:cnt.
  "$pTICK... (turno Nro" say botón:cnt. ")"
  Schedule ticker at hero after 1.
End Event.


--==============================================================================
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--------------------------------------------------------------------------------
--
--                    C Á M A R A   D E   C U A R E N T E N A
--
--------------------------------------------------------------------------------
--* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
-- * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
--==============================================================================

-- The "buggy code quarantine chamber" is a restricted zone were examples of
-- malformed ALAN code are stored, for the sole purpose of testing specific
-- Run-Time MESSAGEs that can't be elicited in normal circumstances.

The bugsRoom IsA location
  Name 'Cámara de cuarentena de código'
  Description
    "This chamber contains various code snippets and test objects." -- @TRANSLATE!
  Entered
    "As you enter the quarantine zone the entrance is automatically
     sealed by a thick led panel sliding from above, insulating the
     external world from the dangerous malformed code snippets which
     are stored in this restricted area of the ALAN Laboratories." -- @TRANSLATE!
  Exit sur to salaDePrueba
    Does
      "As you approach the exit, the room sensors activate the X-rays
       shower to decontaminate you from any residue of buggy code,
       before lifting the led barrier and letting you through." -- @TRANSLATE!
    End exit.
End The.

--==============================================================================
-- Malformed Code Snippets
--==============================================================================

-- Each code snippet corresponds to a specific Run-Time MESSAGEs.

Every code_obj IsA object.
  Description ""
  Definite Article ""     -- Treat them as is having a proper name
  Indefinite Article ""   -- (no articles shown).
End every.

-- To simplify listing the various code snippets, we gather them inside a virtual
-- scenery object which can be examined by the player:

The snippets IsA escenario at bugsRoom.
  Name fragmentos de código.
  Name codigo. Name fragmentos.
  Has artículo "los".
  Container
    Header "Los fragmentos de código son:" -- @TRANSLATE! (check translation)
  Description ""
  Verb examinar
    Does only list this.
  End verb.
End the snippets.

--------------------------------------------------------------------------------
-- Test Verbs
--------------------------------------------------------------------------------

Synonyms ejecuta, ejecute = ejecutar.

-- Default test (no parameters required):

Syntax ejecutar = ejecutar (código)
  Where código IsA code_obj
    Else "You can only execute code snippets!" -- @TRANSLATE!

-- Alternative test verbs accepting parameters to test GNA:

Syntax ejecutar_con = ejecutar (código) con (obj)
  Where código IsA code_obj
    Else "You can only execute code snippets!" -- @TRANSLATE!
  And obj IsA test_obj
    Else "You can execute code with designated test objects!" -- @TRANSLATE!

Syntax ejecutar_con2 = ejecutar (código) con (obj1) y (obj2)
  Where código IsA code_obj
    Else "You can only execute code snippets!" -- @TRANSLATE!
  And obj1 IsA test_obj
    Else "You can execute code with designated test objects!" -- @TRANSLATE!
  And obj2 IsA test_obj
    Else "You can execute code with designated test objects!" -- @TRANSLATE!

-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
-- NOTE: Run-Time MESSAGEs identifiers are not reserved words in ALAN! they have
--       special meaning only inside MESSAGE blocks, and can be freely used as
--       identifiers elsewhere, without name clashing.
-- ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

-- Snippets are created in inverse alphabetical order of the MESSAGEs, so that
-- they will be listed in the correct order (FILO -- first-in last-out).

-- =================
-- CONTAINMENT_LOOP2
-- =================

The CONTAINMENT_LOOP2 IsA code_obj in snippets.
  Has xDesc
    "Executing this code will trigger the CONTAINMENT_LOOP2
     Run-Time MESSAGE by attempting to insert an object into
     another object while the latter is inside the former.". -- @TRANSLATE!

  Verb ejecutar
    Does
      locate cont1 in cont2.
  End verb.

  Verb ejecutar_con2
    Does
      -- -----------------------------------------------------------------------
      -- We need to remove test objects which might have been inserted into each
      -- other by previous executions of this verb. We have to do it here since
      -- this verb will not tick a turn due to failure, so we can't use an
      -- Event to handle this (Events won't be executed between successive
      -- player input triggering this verb) nor can we cater for it at the end
      -- of the verb body, since it will abort execution.
      -- -----------------------------------------------------------------------
      For each tObj IsA test_obj do
        Empty tObj in test_objects.
        Locate tObj in test_objects.
      End for.
      Locate obj2 in obj1.
      Locate obj1 in obj2. -- Verb triggers CONTAINMENT_LOOP2 here and aborts!
  End verb.
End the CONTAINMENT_LOOP2.

-- ================
-- CONTAINMENT_LOOP
-- ================

The CONTAINMENT_LOOP IsA code_obj in snippets.
  Has xDesc
    "Executing this code will trigger the CONTAINMENT_LOOP
     Run-Time MESSAGE by attempting to insert an object
     into itself.". -- @TRANSLATE!

  Verb ejecutar
    Does
      locate cont1 in cont1.
  End verb.

  Verb ejecutar_con
    Does
      locate obj in obj.
  End verb.
End the CONTAINMENT_LOOP.

--------------------------------------------------------------------------------
-- Code Test Objects
--------------------------------------------------------------------------------

Every test_obj IsA escenario container.
  Description ""
End every.

-- To simplify listing the various test objects, we gather them inside a virtual
-- scenery object which can be examined by the player:

The test_objects IsA escenario at bugsRoom.
  Name objetos de prueba.
  Name objetos.
  Has artículo "los".
  Container
    Header "Los objetos de prueba son:" -- @TRANSLATE! (check translation)
  Description ""
  Verb examinar
    Does only list this.
  End verb.
End the test_objects.

-- We need various objects with different number and gender in order to test
-- that the various MESSAGEs are handling GNA properly.

The bidón IsA test_obj in test_objects.   --> m.s. (drum)
  Name bidón. Name bidon.
End the.

The caja IsA test_obj in test_objects.    --> f.s. (box)
  Has artículo "la".
End the.

The bidónes IsA test_obj in test_objects. --> m.p. (drums)
  Name bidónes. Name bidones.
  Has artículo "los".
End the.

The cajas IsA test_obj in test_objects.   --> f.p. (boxs)
  Has artículo "las".
End the.

-- Objects used for default tests, when no parameters are specified.
-- These are not located anywhere since they are for internal VERB use,
-- so the player can't interact with them directly.

The cont1 IsA test_obj container.
  Name 'contenedor designado'.
End the.

The cont2 IsA test_obj container in cont1.
  Name 'contenedor de destino'.
End the.

--------------------------------------------------------------------------------

Start at salaDePrueba.

 "Estás en la Sala de pruebas.
  Aquí se verifica que todo vaya bien con los juegos de ALAN.
  Fuiste escogido para ser un sujeto de pruebas debido a tus extraordinarias
  habilidades (y quizás tu atractiva apariencia tuvo algo que ver).

$pHoy comprobarás que todo marcha bien con ciertos meta-comandos del juego.
  Recuerda que a pesar de haber sido escogido, este trabajo no es remunerado
  en lo absoluto, pero hey, al menos estás siendo útil ¿no?

$p¡Mucha suerte y gracias por contribuir con ALAN!

$p-ALAN Enterprises"
