--==============================================================================
--------------------------------------------------------------------------------
--* * Cloak of Darkness v3 * * 2021/09/26 | Foundation Lib v0.2.2 | ALAN 3.Beta8
--------------------------------------------------------------------------------
--==============================================================================

-- A sample adventure based on an idea by Roger Firth.
-- Originally implemented in ALAN 2 by Stephen Griffiths, then adapted to ALAN 3
-- by Thomas Nilsson (now Thomas Nilefalk) in 2008:
--    http://www.firthworks.com/roger/cloak/alan/
-- Readapted in 2021 by Tristano Ajmone to work with the ALAN Foundation Library:
--    https://github.com/alan-if/alan-i18n
--==============================================================================
Import 'Library.i'.

The foyer IsA location
  Name 'Foyer of the Opera House'

  Description
    "You are standing in a spacious hall, splendidly
     decorated in red and gold, with glittering
     chandeliers overhead.
     The entrance from the street is to the north,
     and there are doorways south and west."

  Exit south to bar
    Does
      If cloak in hero then
        Locate hero at dark_bar.
      else
        Locate hero at bar.
      End if.
  End exit.

  Exit west to cloakroom.

  Exit north to foyer
    Check
      "You've only just arrived, and besides,
       the weather outside seems to be getting worse."
  End exit.
End the foyer.


The cloakroom IsA location
  Description
    "The walls of this small room were clearly once
     lined with hooks, though now only one remains.
     The exit is a door to the east."

  Exit east to foyer.
End the cloakroom.


The hook IsA object
  Name small brass hook
  At cloakroom
  Is not takeable.
  Description

  Verb examine
    Does only
      "It's just a small brass hook"
      If cloak is on_hook then
        "with a cloak hanging on it."
      else
        "screwed to the wall."
      End if.
  End verb.
End the hook.

Synonyms
  peg = hook.


The bar IsA location
  Name foyer bar

  Description
    "The bar, much rougher than you'd have guessed
     after the opulence of the foyer to the north,
     is completely empty.
     There seems to be some sort of message scrawled
     in the sawdust on the floor."

  Exit north to foyer.
End the bar.


The 'message' IsA object
  Name scrawled 'message'
  At bar
  Is not takeable.
  Is readable.
  Has damage_number 0.
  Description

  Verb examine
    Does only
      Describe read_message.
  End verb.

  Verb read
    Does only
      Describe read_message.
  End verb.
End the 'message'.

Synonyms
  floor, sawdust = 'message'.


The read_message IsA object
  Description
    If damage_number of 'message' <2 then
      Score 1.
      "The message, neatly marked in the sawdust,
       reads... $p$t*** You have won ***$p"
    else
      "The message has been carelessly trampled,
       making it difficult to read.
       You can just distinguish the words...
       $p$t*** You have lost ***$p"
    End if.
    Score.
    Quit.
End the read_message.

The dark_bar IsA location
  Name darkness

  Description
    "It's pitch black here. You can't see a thing."

  Exit north to foyer.

  Exit northeast, east, southeast, south,
       southwest, west, northwest
       to dark_bar
    Check
      "Blundering around in the dark isn't a good idea!"
      Increase damage_number of 'message' by 2.
  End exit.

  Verb invent, 'look', 'wait'
    Does only
      Describe dark_warning.
  End verb.

  Verb read, switch_on1, switch_on2,
       turn_on1, turn_on2
    Does only
      Describe dark_warning.
  End verb.

  Verb drop, examine, pick_up1, pick_up2,
       put_down1, put_down2, take, throw
    Does only
      Describe dark_warning.
  End verb.

  Verb search
    Does only
      Describe dark_warning.
  End verb.
End the dark_bar.


The dark_warning IsA object
  Description
    "In the dark? You could easily disturb something!"
    Increase damage_number of 'message' by 1.
End the dark_warning.


The cloak IsA object
  Name black velvet cloak
  In hero
  Is not on_hook.
  Description
    If cloak is on_hook then
      "$pOn the small brass hook is a velvet cloak."
    else
      "$pThere is a black velvet cloak here."
    End if.
  Has xDesc "A handsome cloak, of velvet trimmed with
             satin, and slightly spattered with raindrops.
             Its blackness is so deep that it almost seems
             to suck light from the room.".

  Verb examine
    Check Current Location <>dark_bar
      else describe dark_warning.
  End verb.

  Verb drop, put_down1, put_down2
    Check Current Location <>dark_bar
      else describe dark_warning.
    And Current Location =cloakroom
      else "This isn't the best place to leave
            a smart cloak lying around."
  End verb.

  Verb put_on
    When obj1
      Check obj2 =hook
        else "This isn't the best place to leave
              a smart cloak lying around."
      Does only
        If hero is not hung_cloak then
          Make hero hung_cloak.
          Score 1.
        End if.
        "You $v the $1 on the $2."
        Make cloak on_hook.
        Locate cloak here.
  End verb.
End the cloak.

Synonyms
  hang = put.


Add to every actor
  Is not hung_cloak.
  Has xDesc "As handsome as ever.".
End Add.

Add to every location
  Verb examine
    Check current location <> dark_bar
      else Describe dark_warning.
  End verb.
End Add.


Start at foyer.
  "$p$pHurrying through the rainswept November night,
   you're glad to see the bright lights of the Opera House.
   It's surprising that there aren't more people about but,
   hey, what do you expect in a cheap demo game...?"
  Style alert.
  "$p$nCloak of Darkness" Style emphasized.
  "$nA basic IF demonstration." Style normal.
  "$pVersion 3, September 2021. (ALAN 3.0Beta8, Foundation Library v0.2.2)."
  "$nOriginally implemented by Stephen Griffiths for ALAN 2,
   readapted by the ALAN IF Development Team."
